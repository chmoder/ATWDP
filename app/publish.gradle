apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.1.0'
group 'org.chmoder'

// Task to generate javadoc
task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

// Task to generate javadoc
task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += configurations.compile
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// Task to generate javadoc JAR
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/app-release.aar")
			artifact javadocJar
			artifact sourcesJar
			
            groupId
            artifactId 'ATWDP'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = 'chmoder'
    key = ''
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'ATWDP'
        name = 'ATWDP'
        userOrg = 'chmoder'
        description = "Android Things Wireless Device Provisioning"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/chmoder/ATWDP.git'
//        dryRun = true
        version {
            name = this.version
            desc = "ATWDP ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}